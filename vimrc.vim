"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"
"       Install the awesome version from:
"
"           https://github.com/amix/vimrc
"
" Syntax_highlighted:
"       http://amix.dk/vim/vimrc.html
"
" Raw_version: 
"       http://amix.dk/vim/vimrc.txt
"
" Sections:
"    -> General
"    -> VIM user interface
"    -> Colors and Fonts
"    -> Files and backups
"    -> Text, tab and indent related
"    -> Folding
"    -> Visual mode related
"    -> Moving around, tabs and buffers
"    -> Status line
"    -> Editing mappings
"    -> vimgrep searching and cope displaying
"    -> Spell checking
"    -> Misc
"    -> Helper functions
"    -> Vundle plugins
"    -> Crypto
"
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => General
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Sets how many lines of history VIM has to remember
set history=700

set nocompatible               " be iMproved

" Enable filetype plugins
filetype plugin on
filetype indent on

"call pathogen#infect()

" Set to auto read when a file is changed from the outside
set autoread

" With a map leader it's possible to do extra key combinations
" like <leader>w saves the current file
let mapleader = ","
let g:mapleader = ","

" Fast saving
nmap <leader>w :w!<cr>


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => VIM user interface
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Set 7 lines to the cursor - when moving vertically using j/k
set so=7

" Turn on the WiLd menu
set wildmenu

" Ignore compiled files
set wildignore=*.o,*~,*.pyc

"Always show current position
set ruler

" Height of the command bar
set cmdheight=2

" A buffer becomes hidden when it is abandoned
set hid

" Configure backspace so it acts as it should act
set backspace=eol,start,indent
set whichwrap+=<,>,h,l

" Ignore case when searching
set ignorecase

" When searching try to be smart about cases 
set smartcase

" Highlight search results
set hlsearch

" Makes search act like search in modern browsers
set incsearch

" Don't redraw while executing macros (good performance config)
set lazyredraw

" For regular expressions turn magic on
set magic
" show line numbers
set nu

" Show matching brackets when text indicator is over them
set showmatch
" How many tenths of a second to blink when matching brackets
set mat=2

" No annoying sound on errors
set noerrorbells
set novisualbell
set t_vb=
set tm=500


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Colors and Fonts
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Enable syntax highlighting
syntax enable

" Show comments in Blue
highlight Comment ctermfg=DarkBlue

" Set extra options when running in GUI mode
if has("gui_running")
    " https://github.com/morhetz/gruvbox/wiki/Installation
    autocmd vimenter * ++nested colorscheme gruvbox

    " colorscheme gruvbox
    set background=dark
    set guioptions-=T
    set guioptions+=e
    set t_Co=256
    set guitablabel=%M\ %t
    set guifont=Hack\ 11
endif

" Set utf8 as standard encoding and en_US as the standard language
set encoding=utf8

" Use Unix as the standard file type
set ffs=unix,dos,mac


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Files, backups and undo
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Turn backup off, since most stuff is in SVN, git et.c anyway...
set nobackup
set nowb
set noswapfile


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Text, tab and indent related
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Use spaces instead of tabs
set expandtab

" Be smart when using tabs ;)
set smarttab

" 1 tab == 4 spaces
set shiftwidth=4
set tabstop=4

" Linebreak on 500 characters
set lbr
set tw=500

set ai "Auto indent
set si "Smart indent
set wrap "Wrap lines

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" FOLDING
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" enable folding
set foldenable

" foldlevelstart is the starting fold level for opening a new buffer. If it is set to 0, all folds will be closed. Setting it to 99 would guarantee folds are always open. So, setting it to 10 here ensures that only very nested blocks of code are folded when opening a buffer.
set foldlevelstart=10


"This tells Vim to fold based on indentation. This is especially useful for me since I spend my days in Python. Other acceptable values are marker, manual, expr, syntax, diff. Run :help foldmethod to find out what each of those do.
set foldmethod=indent


""""""""""""""""""""""""""""""""""""""""
" => remap ctrl-a ctrl-x to alt-a, alt-x
"""""""""""""""""""""""""""""""""""""""""
":nnoremap <A-a> <C-a>
":nnoremap <A-x> <C-x>
"
""""""""""""""""""""""""""""""
" => Visual mode related
""""""""""""""""""""""""""""""
" Visual mode pressing * or # searches for the current selection
" Super useful! From an idea by Michael Naumann
vnoremap <silent> * :call VisualSelection('f')<CR>
vnoremap <silent> # :call VisualSelection('b')<CR>


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Moving around, tabs, windows and buffers
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Treat long lines as break lines (useful when moving around in them)
map j gj
map k gk

" Map <Space> to / (search) and Ctrl-<Space> to ? (backwards search)
map <space> /
map <c-space> ?

" Disable highlight when <leader><cr> is pressed
map <silent> <leader><cr> :noh<cr>

" Smart way to move between windows
map <C-j> <C-W>j
map <C-k> <C-W>k
map <C-h> <C-W>h
map <C-l> <C-W>l

" Close the current buffer
map <leader>bd :Bclose<cr>

" Close all the buffers
map <leader>ba :1,1000 bd!<cr>

" Useful mappings for managing tabs
map <leader>tn :tabnew<cr>
map <leader>to :tabonly<cr>
map <leader>tc :tabclose<cr>
map <leader>tm :tabmove

" Opens a new tab with the current buffer's path
" Super useful when editing files in the same directory
map <leader>te :tabedit <c-r>=expand("%:p:h")<cr>/

" Switch CWD to the directory of the open buffer
map <leader>cd :cd %:p:h<cr>:pwd<cr>

" Specify the behavior when switching between buffers 
try
  set switchbuf=useopen,usetab,newtab
  set stal=2
catch
endtry

" Return to last edit position when opening files (You want this!)
autocmd BufReadPost *
     \ if line("'\"") > 0 && line("'\"") <= line("$") |
     \   exe "normal! g`\"" |
     \ endif
" Remember info about open buffers on close
set viminfo^=%

" Switch to alternate file, all-tab approach
map <C-Tab> :bnext<cr>
map <C-S-Tab> :bprevious<cr>

" Buffer settings for buffer airline plugin 
" Enable the list of buffers
let g:airline#extensions#tabline#enabled = 1

" Show just the filename
let g:airline#extensions#tabline#fnamemod = ':t'

" enable/disable displaying tab number in tabs mode. >
let g:airline#extensions#tabline#show_tab_nr = 1


" highlight last inserted text
nnoremap gV `[v`]
""""""""""""""""""""""""""""""
" => Status line
""""""""""""""""""""""""""""""
" Always show the status line
set laststatus=2

" Format the status line
set statusline=\ %{HasPaste()}%F%m%r%h\ %w\ \ CWD:\ %r%{getcwd()}%h\ \ \ Line:\ %l


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Editing mappings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Remap VIM 0 to first non-blank character
map 0 ^

" Move a line of text using ALT+[jk] or Comamnd+[jk] on mac
nmap <M-j> mz:m+<cr>`z
nmap <M-k> mz:m-2<cr>`z
vmap <M-j> :m'>+<cr>`<my`>mzgv`yo`z
vmap <M-k> :m'<-2<cr>`>my`<mzgv`yo`z

if has("mac") || has("macunix")
  nmap <D-j> <M-j>
  nmap <D-k> <M-k>
  vmap <D-j> <M-j>
  vmap <D-k> <M-k>
endif

" Delete trailing white space on save, useful for Python and CoffeeScript ;)
func! DeleteTrailingWS()
  exe "normal mz"
  %s/\s\+$//ge
  exe "normal `z"
endfunc
autocmd BufWrite *.py :call DeleteTrailingWS()
autocmd BufWrite *.coffee :call DeleteTrailingWS()


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => vimgrep searching and cope displaying
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" When you press gv you vimgrep after the selected text
vnoremap <silent> gv :call VisualSelection('gv')<CR>

" Open vimgrep and put the cursor in the right position
map <leader>g :vimgrep // **/*.<left><left><left><left><left><left><left>

" Vimgreps in the current file
map <leader><space> :vimgrep // <C-R>%<C-A><right><right><right><right><right><right><right><right><right>

" When you press <leader>r you can search and replace the selected text
vnoremap <silent> <leader>r :call VisualSelection('replace')<CR>

" Do :help cope if you are unsure what cope is. It's super useful!
"
" When you search with vimgrep, display your results in cope by doing:
"   <leader>cc
"
" To go to the next search result do:
"   <leader>n
"
" To go to the previous search results do:
"   <leader>p
"
map <leader>cc :botright cope<cr>
map <leader>co ggVGy:tabnew<cr>:set syntax=qf<cr>pgg
map <leader>n :cn<cr>
map <leader>p :cp<cr>


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Spell checking
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Pressing ,ss will toggle and untoggle spell checking
map <leader>ss :setlocal spell!<cr>

" Shortcuts using <leader>
map <leader>sn ]s
map <leader>sp [s
map <leader>sa zg
map <leader>s? z=


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Misc
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Remove the Windows ^M - when the encodings gets messed up
noremap <Leader>m mmHmt:%s/<C-V><cr>//ge<cr>'tzt'm

" Quickly open a buffer for scripbble
map <leader>q :e ~/buffer<cr>

" Toggle paste mode on and off
map <leader>pp :setlocal paste!<cr>

" After saving a Vim session, you can reopen it with vim -S.  
" save session
nnoremap <leader>s :mksession<CR>


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Helper functions
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! CmdLine(str)
    exe "menu Foo.Bar :" . a:str
    emenu Foo.Bar
    unmenu Foo
endfunction

function! VisualSelection(direction) range
    let l:saved_reg = @"
    execute "normal! vgvy"

    let l:pattern = escape(@", '\\/.*$^~[]')
    let l:pattern = substitute(l:pattern, "\n$", "", "")

    if a:direction == 'b'
        execute "normal ?" . l:pattern . "^M"
    elseif a:direction == 'gv'
        call CmdLine("vimgrep " . '/'. l:pattern . '/' . ' **/*.')
    elseif a:direction == 'replace'
        call CmdLine("%s" . '/'. l:pattern . '/')
    elseif a:direction == 'f'
        execute "normal /" . l:pattern . "^M"
    endif

    let @/ = l:pattern
    let @" = l:saved_reg
endfunction


" Returns true if paste mode is enabled
function! HasPaste()
    if &paste
        return 'PASTE MODE  '
    en
    return ''
endfunction

" Don't close window, when deleting a buffer
command! Bclose call <SID>BufcloseCloseIt()
function! <SID>BufcloseCloseIt()
   let l:currentBufNum = bufnr("%")
   let l:alternateBufNum = bufnr("#")

   if buflisted(l:alternateBufNum)
     buffer #
   else
     bnext
   endif

   if bufnr("%") == l:currentBufNum
     new
   endif

   if buflisted(l:currentBufNum)
     execute("bdelete! ".l:currentBufNum)
   endif
endfunction

"http://vim.wikia.com/wiki/Diff_current_buffer_and_the_original_file
"function and command to see a diff  between the currently edited file and its unmodified version in the filesystem. 
" use :DiffSaved
" to turn diff view off : diffoff

function! s:DiffWithSaved()
    let filetype=&ft
    diffthis
    vnew | r # | normal! 1Gdd
    diffthis
    exe "setlocal bt=nofile bh=wipe nobl noswf ro ft=" . filetype
endfunction
com! DiffSaved call s:DiffWithSaved()

"""""""""""""""""""""""""""""""""""""""""""""""""""""
"PLUG 
""""""""""""""""""""""""""""""""""""""""""""""""""""""
" https://github.com/junegunn/vim-plug

filetype off                   " required!
 " install Plug
 let data_dir = has('nvim') ? stdpath('data') . '/site' : '~/.vim'
 if empty(glob(data_dir . '/autoload/plug.vim'))
  silent execute '!curl -fLo '.data_dir.'/autoload/plug.vim --create-dirs  https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
 endif

 call plug#begin()
 " My Bundles here:
 "
 " original repos on github
 Plug 'tpope/vim-fugitive'
 Plug 'easymotion/vim-easymotion'
 Plug 'rstacruz/sparkup', {'rtp': 'vim/'}
 Plug 'pangloss/vim-javascript'    
 Plug 'tmhedberg/SimpylFold' 
 Plug 'dart-lang/dart-vim-plugin'
 " The Silver Searcher is a fantastic command line tool to search source code in a project. WICKED FAST 
 "Bundle 'rking/ag.vim' 
 " Gundo displaying that undo tree in graphical form. 
 Plug 'sjl/gundo.vim'
 Plug 'kien/ctrlp.vim'

 "C IDE
 Plug 'vim-scripts/c.vim'

 "Bundle 'vim-scripts/ShowMarks'
 Plug 'tmhedberg/matchit'
 "Bundle 'chazy/cscope_maps'

 " non github repos
 Plug 'mbbill/undotree'
 "Bundle 'klen/python-mode.git'
 Plug 'Lokaltog/vim-powerline'

 "nerd commentor
 Plug 'scrooloose/nerdcommenter'
 Plug 'scrooloose/nerdtree'
 "Bundle 'scrooloose/syntastic'     " using ale, its async
 "if has("gui_running")
     Plug 'w0rp/ale'
 "endif

 "Bundle 'git://git.wincent.com/command-t.git'
 Plug 'bling/vim-airline' 

 " autocompletion
 Plug 'Valloric/YouCompleteMe'

 "Markdown folding
 Plug 'nelstrom/vim-markdown-folding'

 " Lightning fast left-right movement in VIM
 Plug 'unblevable/quick-scope'

 Plug 'kelwin/vim-smali'
 Plug 'preservim/tagbar'

 " color scheme
 Plug 'morhetz/gruvbox'

 call plug#end()

 


 filetype plugin indent on   

 """""""""""""""""""""""""""""""
 " VIM-NOTES + vim-pad + vimwiki
 """""""""""""""""""""""""""""""
 "let g:notes_directories = ['~/Documents/OneDrive/notes']
 "let g:notes_suffix = '.md'
 let g:vimwiki_list = [{'path': '~/Documents/OneDrive/notes', 'syntax':'markdown', 'ext':'.md'}]

""""""""""""""""""""""""""
 " MARKDOWN 
 """"""""""""""""""""""""""""
 let g:vim_markdown_toc_autofit = 1
 " change By default text emphasis works across multiple lines until a closing token is found.
 let g:vim_markdown_emphasis_multiline = 0

 "autocmd FileType vimwiki set ft=markdown
 " Vimwiki dont think all markdowns are vimwiki files
 let g:vimwiki_global_ext = 0
 """"""""""""""""""""""""""
 " ALE
 """"""""""""""""""""""""""""
 " dont run when you open a file
 let g:ale_lint_on_enter = 0
 " RUn only when file is saved
 let g:ale_lint_on_text_changed = 'never'
 let g:ale_fixers = {
 \'*': ['remove_trailing_lines', 'trim_whitespace', 'prettier'],
 \}
 let g:ale_linters = {
             \'markdown': ['mdl', 'markdownlint'],
             \'md': ['mdl', 'markdownlint'],
             \'javascript': ['eslint'],
             \}
 

 """"""""""""""""""""""""""
 " CTRL P
 """"""""""""""""""""""""""""
 " CtrlP settings
let g:ctrlp_match_window = 'bottom,order:ttb'
let g:ctrlp_switch_buffer = 0
let g:ctrlp_working_path_mode = 0
let g:ctrlp_user_command = 'ag %s -l --nocolor --hidden -g ""'

 """""""""""""""""""""""""""
 " AG SILVER SEARCHER 
 """"""""""""""""""""""""""""
 " open ag.vim
 "nnoremap <leader>a :Ag

" The Silver Searcher
" If ag is there, use it instead of grep
"if executable('ag')
  " Use ag over grep
  "set grepprg=ag\ --nogroup\ --nocolor

  " Use ag in CtrlP for listing files. Lightning fast and respects .gitignore
  "let g:ctrlp_user_command = 'ag %s -l --nocolor -g ""'

  " ag is fast enough that CtrlP doesn't need to cache
  "let g:ctrlp_use_caching = 0
"endif
""""""""""""""""""""""""""""""
" QUICK SCOPE
"""""""""""""""""""""""""""""""

" Trigger a highlight in the appropriate direction when pressing these keys:
let g:qs_highlight_on_keys = ['f', 'F', 't', 'T']

" Trigger a highlight only when pressing f and F.
let g:qs_highlight_on_keys = ['f', 'F']


 """""""""""""""""'
 " PYMODE 
 """""""""""""""

 " Line length
 let g:pymode_options_max_line_length = 120

 " to solve rope cachine issue
 let g:pymode_rope = 0
 let g:pymode_rope_lookup_project = 0

 " Documentation
 let g:pymode_doc = 1
 let g:pymode_doc_key = 'K'

 "Linting
 let g:pymode_lint = 1
 let g:pymode_lint_checker = "pyflakes,pep8"

 " Auto check on save
 let g:pymode_lint_write = 1
 
 " Support virtualenv
 let g:pymode_virtualenv = 1

 " Enable breakpoints plugin
 let g:pymode_breakpoint = 1
 let g:pymode_breakpoint_bind = '<leader>b'

 " syntax highlighting
 let g:pymode_syntax = 1
 let g:pymode_syntax_all = 1
 let g:pymode_syntax_indent_errors = g:pymode_syntax_all
 let g:pymode_syntax_space_errors = g:pymode_syntax_all

 let g:pymode_python = 'python3'

 """"""""""""""""""""""""""""""""
 " NERDTREE CONFIG
 """"""""""""""""""""""""""""""""
 " Map ctrl-n to open nerdtree
 map <C-n> :NERDTreeToggle<CR>    

 """"""""""""""""""""""""""""""""""""
 " GUNDO
 """""""""""""""""""""""""""""""""
 " toggle gundo
nnoremap <leader>u :GundoToggle<CR>

 """""""""""""""""""""""""""""""""
 "Syntastic setup
 """"""""""""""""""""""""""""""""
  "Use flake8
let g:syntastic_python_checkers = ['flake8']
let g:syntastic_python_flake8_args = '--ignore="E501,E302,E261,E701,E241,E126,E127,E128,W801"'
let g:syntastic_markdown_mdl_exec = 'markdownlint'
let g:syntastic_markdown_mdl_args = ''

 """""""""""""""""""""""""""""""""""
 "DOXYGEN
 """""""""""""""""""""""""""""""""""
 "let g:DoxygenToolkit_authorName=“Vikas Gupta <vikasgupta.nit@gmail.com>”
 "in case to get rid of highlighted text
 let @/=""

 """"""""""""""""""""""""""""""""""""""
 " AUTHOR NAME, using: http://www.vim.org/scripts/script.php?script_id=2902
 " """"""""""""""""""""""""""""""""""""
let g:vimrc_author='Vikas Gupta' 
let g:vimrc_email='vikasgupta.nit@gmail.com' 
let g:vimrc_homepage=' '
nmap <F4> :AuthorInfoDetect<cr>
 

"""""""""""""""""""""""""""""""""""""""""""""""""""""
"CRYPTO 
""""""""""""""""""""""""""""""""""""""""""""""""""""""
"set cryptmethod=blowfish2

